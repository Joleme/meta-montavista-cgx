# Copyright (c) 2013 MontaVista Software, Inc.  All rights reserved.
#
# Released under the MIT license (see LICENSE.MIT for the terms)
#

PR .= ".3"

cpan_do_configure () {
	# This is in the openembedded-core cpan configure, but it causes
	# a failure.  This doesn't make any sense, you shouldn't be using
	# the target perl libraries with the host perl, if it picks up a
	# shared object it will be wrong.  That's what was happening with
	# this package.
	# export PERL5LIB="${PERL_ARCHLIB}"
	yes '' | perl ${EXTRA_PERLFLAGS} Makefile.PL ${EXTRA_CPANFLAGS}

	# Makefile.PLs can exit with success without generating a
	# Makefile, e.g. in cases of missing configure time
	# dependencies. This is considered a best practice by
	# cpantesters.org. See:
	#  * http://wiki.cpantesters.org/wiki/CPANAuthorNotes
	#  * http://www.nntp.perl.org/group/perl.qa/2008/08/msg11236.html
	[ -e Makefile ] || bbfatal "No Makefile was generated by Makefile.PL"

	if [ "${BUILD_SYS}" != "${HOST_SYS}" ]; then
		. ${STAGING_LIBDIR}${PERL_OWN_DIR}/perl/config.sh
		# Use find since there can be a Makefile generated for each Makefile.PL
		for f in `find -name Makefile.PL`; do
			f2=`echo $f | sed -e 's/.PL//'`
			test -f $f2 || continue
			sed -i -e "s:\(PERL_ARCHLIB = \).*:\1${PERL_ARCHLIB}:" \
				-e 's/perl.real/perl/' \
				-e "s:^\(CCFLAGS =.*\):\1 ${CFLAGS}:" \
				$f2
		done
	fi
}

do_compile_prepend () {
	sed -i "s|^LD_RUN_PATH.*||g" ${S}/Expat/Makefile
}
